-- QTV04/ NHẬP THUỐC
CREATE PROCEDURE SP_NHAPTHEMTHUOC_QTV
    @MATHUOC VARCHAR(10),
    @SOLUONGNHAP INT,
    @NGAYHETHAN DATE
AS
BEGIN TRAN
BEGIN TRY 
BEGIN
    SET NOCOUNT ON;
    IF @SOLUONGNHAP < 1
    BEGIN
        RAISERROR(N'Số lượng nhập phải lớn hơn 0', 16, 1)
        ROLLBACK TRAN
        RETURN
    END

	IF @NGAYHETHAN <= GETDATE()
    BEGIN
        RAISERROR(N'Ngày hết hạn không hợp lệ', 16, 1)
        ROLLBACK TRAN
        RETURN
    END


    DECLARE @SLTON_OLD INT, @SLTON_NEW INT, @SLNHAP_OLD INT, @SLNHAP_NEW INT, @NGAYHETHAN_OLD DATE;

    SELECT @SLTON_OLD = ISNULL(SLTON, 0), @NGAYHETHAN_OLD = NGAYHETHAN, @SLNHAP_OLD = SLNHAP
    FROM LOAITHUOC WITH (UPDLOCK)
    WHERE MATHUOC = @MATHUOC;


    IF @SLTON_OLD = 0
    BEGIN
        SET @SLTON_NEW = @SOLUONGNHAP;
        SET @SLNHAP_NEW = @SLNHAP_OLD + @SOLUONGNHAP;

        UPDATE LOAITHUOC
        SET SLTON = @SLTON_NEW, SLNHAP = @SLNHAP_NEW, NGAYHETHAN = @NGAYHETHAN
        WHERE MATHUOC = @MATHUOC;

    END
    ELSE IF @NGAYHETHAN_OLD <= GETDATE() 
    BEGIN
        SET @SLTON_NEW = @SOLUONGNHAP;
        SET @SLNHAP_NEW = @SLNHAP_OLD + @SOLUONGNHAP;

        UPDATE LOAITHUOC
        SET SLTON = @SLTON_NEW, SLNHAP = @SLNHAP_NEW, NGAYHETHAN = @NGAYHETHAN, SLDAHUY = SLDAHUY + @SLTON_OLD
        WHERE MATHUOC = @MATHUOC;
    END
    ELSE
    BEGIN
        RAISERROR(N'Không thể nhập vì thuốc chưa hết hạn hoặc chưa hết số lượng.',16,1);
        ROLLBACK TRAN
        RETURN
    END
END;
END TRY 
BEGIN CATCH 
        ROLLBACK TRAN;
		DECLARE @errorMessage NVARCHAR(200) = ERROR_MESSAGE();
		THROW 51000, @errorMessage, 1
		RETURN
END CATCH
COMMIT TRAN
GO

-- QTV02/ HỦY LOẠI THUỐC
CREATE PROCEDURE SP_HUYTHUOC_QTV
    @MATHUOC VARCHAR(10)
AS
BEGIN TRAN
BEGIN TRY 
BEGIN
    DECLARE @NGAYHETHAN DATE;

    SELECT @NGAYHETHAN = NGAYHETHAN
    FROM LOAITHUOC WITH (UPDLOCK)
    WHERE MATHUOC = @MATHUOC;

    IF @NGAYHETHAN < GETDATE()
    BEGIN

        UPDATE LOAITHUOC
        SET SLDAHUY = SLDAHUY + SLTON, SLTON = 0
        WHERE MATHUOC = @MATHUOC;
    END
    ELSE
    BEGIN
        RAISERROR(N'Không thể hủy thuốc vì chưa hết hạn.',16,1);
        ROLLBACK TRAN
        RETURN
    END
END;
END TRY 
BEGIN CATCH 
        ROLLBACK TRAN;
		DECLARE @errorMessage NVARCHAR(200) = ERROR_MESSAGE();
		THROW 51000, @errorMessage, 1
		RETURN
END CATCH
COMMIT TRAN
GO

-------------------------------------------------------
--ALL05/ TẠO LỊCH HẸN
GO
CREATE PROC SP_DATLICHHEN_NV_KH
	@SODT VARCHAR(100),
	@MANS VARCHAR(100),
	@SOTT INT,
	@LYDOKHAM NVARCHAR(200)
AS
BEGIN TRAN
	BEGIN TRY
		IF (EXISTS(SELECT * 
				   FROM LICHHEN WITH(UPDLOCK, HOLDLOCK)
				   WHERE MANS = @MANS AND SOTT = @SOTT))
		BEGIN
			RAISERROR(N'Lỗi: Đã có khách hàng đặt lịch hẹn này.',16,1)
			ROLLBACK TRAN
			RETURN
		END

		IF (EXISTS(SELECT LH.*
				  FROM LICHHEN LH WITH(UPDLOCK, HOLDLOCK) JOIN LICHRANH LR WITH(UPDLOCK, HOLDLOCK)
 				  ON LH.MANS = LR.MANS AND LH.SOTT = LR.SOTT
				  WHERE EXISTS(SELECT *
			 	  			   FROM LICHRANH LR2 WITH(UPDLOCK, HOLDLOCK)
			 				   WHERE LR2.MANS != LH.MANS AND LR.NGAY = LR2.NGAY AND LR.MACA = LR2.MACA
			 				   AND LH.SODT = @SODT
			 				   AND LR2.SOTT = @SOTT
			 				   AND LR2.MANS = @MANS)))
		BEGIN
			RAISERROR(N'Lỗi: Các lịch hẹn của cùng một khách hàng không được trùng ca nhau.',16,1)
			ROLLBACK TRAN
			RETURN
		END

		ELSE
		BEGIN
			INSERT INTO LICHHEN(MANS, SODT, LYDOKHAM, SOTT) 
			VALUES(@MANS, @SODT, @LYDOKHAM, @SOTT)
		END
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN;
		DECLARE @errorMessage NVARCHAR(200) = ERROR_MESSAGE();
		THROW 51000, @errorMessage, 1;
		RETURN
	END CATCH
COMMIT TRAN

--------------------------
--NV01/ XEM LỊCH TRỰC TỪ NGÀY HIỆN TẠI ĐẾN 7 NGÀY KẾ CỦA NHA SĨ
GO
CREATE PROC SP_GETLICHRANHNS_NV
AS
BEGIN TRAN
    SET TRANSACTION ISOLATION LEVEL SERIALIZABLE
	BEGIN TRY
		-- LICH CO HEN CUA NHA SI
		SELECT NGAY, CA.MACA, GIOBATDAU, GIOKETTHUC, LH.SOTT SOTTLH, LH.MANS, NS.HOTEN HOTENNS, KH.SODT SODTKH, KH.HOTEN HOTENKH, LH.LYDOKHAM LYDOKHAM
		FROM LICHHEN LH
		JOIN NHASI NS ON NS.MANS = LH.MANS
		JOIN LICHRANH LR2 ON LR2.MANS = LH.MANS AND LH.SOTT = LR2.SOTT
		JOIN CA ON CA.MACA = LR2.MACA
		JOIN KHACHHANG KH ON KH.SODT = LH.SODT
		WHERE DATEDIFF(DAY,GETDATE(), NGAY) <= 7
		ORDER BY NGAY 

		-- LICH CHUA CO HEN CUA NHA SI
		SELECT NGAY, CA.MACA, GIOBATDAU, GIOKETTHUC, LR.SOTT SOTTLR, LR.MANS, HOTEN HOTENNS
		FROM LICHRANH LR
		JOIN CA ON CA.MACA = LR.MACA
		JOIN NHASI NS ON NS.MANS = LR.MANS
		WHERE NOT EXISTS 
		(
			SELECT 1
			FROM LICHHEN LH
			WHERE LH.MANS = LR.MANS AND LH.SOTT = LR.SOTT
		)
		AND DATEDIFF(DAY,GETDATE(), NGAY) <= 7
		ORDER BY NGAY 
	END TRY
	BEGIN CATCH
		ROLLBACK TRAN;
		DECLARE @errorMessage NVARCHAR(200) = ERROR_MESSAGE();
		THROW 51000, @errorMessage, 1;
		RETURN
	END CATCH
COMMIT TRAN